plugins {
    id 'java'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

ext {
    slf4jVersion = "2.0.13"
    log4jVersion = "2.23.1"
    junitVersion = "5.10.0"
}

def dataDir = "${rootProject.projectDir}/data"

allprojects {
    group = 'dev.atomixsoft.solar_eclipse'
    version = '0.2.3.1-alpha'

    repositories {
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
}

subprojects {
    apply plugin: 'java'

    dependencies {
        implementation 'org.slf4j:slf4j-api:' + slf4jVersion
        implementation 'org.apache.logging.log4j:log4j-core:' + log4jVersion
        implementation 'org.apache.logging.log4j:log4j-api:' + log4jVersion
        implementation 'org.apache.logging.log4j:log4j-slf4j-impl:' + log4jVersion

        testImplementation platform('org.junit:junit-bom:' + junitVersion)
        testImplementation 'org.junit.jupiter:junit-jupiter'
    }

    plugins.withId('application') {
        tasks.named('run', JavaExec).configure {
            doFirst {
                standardInput = System.in
                workingDir = file(dataDir)
            }
        }
    }

    tasks.withType(JavaExec).tap {
        configureEach {
            doFirst {
                classpath = files(dataDir) + classpath
                ignoreExitValue = true
            }
        }
    }

    tasks.withType(Test).tap {
        configureEach {
            useJUnitPlatform()
        }
    }
}
