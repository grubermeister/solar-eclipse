plugins {
    id 'application'
}

ext {
    jomlVersion = "1.10.5"
    lwjglVersion = "3.3.3"
    imguiVersion = "1.86.11"
}

def dataDir = "${rootProject.projectDir}/data"

def osName = System.getProperty("os.name").toLowerCase()
def lwjglNatives = ""
def imguiNatives = ""

if (osName.contains("linux")) {
    lwjglNatives = "natives-linux"
} else if (osName.contains("mac")) {
    lwjglNatives = "natives-macos"
} else {
    lwjglNatives = "natives-windows"
}

imguiNatives = lwjglNatives


dependencies {
    implementation project(':core')

    implementation "org.joml:joml:$jomlVersion"
    implementation "org.lwjgl:lwjgl:$lwjglVersion"
    implementation "org.lwjgl:lwjgl-glfw:$lwjglVersion"
    implementation "org.lwjgl:lwjgl-opengl:$lwjglVersion"
    implementation "org.lwjgl:lwjgl-openal:$lwjglVersion"
    implementation "org.lwjgl:lwjgl-stb:$lwjglVersion"

    implementation "io.github.spair:imgui-java-binding:$imguiVersion"
    implementation "io.github.spair:imgui-java-lwjgl3:$imguiVersion"
    implementation "io.github.spair:imgui-java-$lwjglNatives:$imguiVersion"

    runtimeOnly "org.lwjgl:lwjgl:$lwjglVersion:$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw:$lwjglVersion:$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl:$lwjglVersion:$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-openal:$lwjglVersion:$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb:$lwjglVersion:$lwjglNatives"

    runtimeOnly "io.github.spair:imgui-java-lwjgl3:$imguiVersion"
    runtimeOnly "io.github.spair:imgui-java-$imguiNatives:$imguiVersion"

}

application {
    mainClass = 'dev.atomixsoft.solar_eclipse.client.Client'
}

task debug(type: JavaExec) {
    doFirst {
        classpath = files(dataDir) + sourceSets.main.runtimeClasspath
        workingDir = file(dataDir)

        println "Classpath is set to ${classpath.asPath}"
        println "Working directory is $workingDir"
    }

    group = 'application'
    description = 'Runs the Solar Eclipse Client in debug mode'

    standardInput = System.in

    mainClass = application.mainClass.get()
    
    jvmArgs = [
        "-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=*:5007"
    ]
    ignoreExitValue = true

    doLast {
        logging.level = LogLevel.INFO
        println "Debugging Solar Eclipse Client..."
    }
}

tasks.named('run', JavaExec) {
    group = 'application'
    description = 'Runs the Solar Eclipse Client'
}
